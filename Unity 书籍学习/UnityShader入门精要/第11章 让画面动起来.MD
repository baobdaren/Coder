# 第十一章 让画面动起来

## Unity Shader中的内置变量（时间篇）  

动画效果往往需要时间，下面是Unity提供的一些时间函数

名称|类型|描述
-:|:-:|:-
_Time|float4|（t/20，t，2t，3t），同时自场景加载开始的时间
_SinTime|float4|（t/8，t/4，4/2，t），t是时间的正弦值。
_CosTime|float4|（t/8，t/4，4/2，t），t是时间的余弦值。
unity_DeltaTime|float4|（dt，1/dt，smoothdt，1/smooth），dt是时间增量。

## 帧动画

帧动画的原理就是一张图片当作了图集，包含一个动画的关键帧，在shander中依赖时间，来连续渲染这些小图片。  
下面的代码实现了爆炸效果的帧动画

```c#
Shader "Custom/Shader"
{
    Properties
    {
        _Color ("Color", Color) = (1,1,1,1)
        _MainTex ("Image Sequence", 2D) = "white" {}
        _HorizontalAmount ("Horizontal Amount", float) = 4
        _VerticalAmount ("VerticalAmount", float) = 4
        _Speed ("Speed", Range(1,100)) = 30
    }
    SubShader
    {
        // 由于序列帧通常含有透明，所以需要设置
        Tags { "IgnoreProjector"="True" "Queue" = "Transparent" "RenderType"="Transparent" }
        pass{
            Tags { "LightMode"="ForwardBase" }
            ZWrite Off
            // 设置混合模式
            Blend SrcAlpha OneMinusSrcAlpha

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"

            fixed4 _Color;
            sampler2D _MainTex;
            float4 _MainTex_ST;
            int _HorizontalAmount;
            int _VerticalAmount;
            float _Speed;

            struct a2v{
                float4 pos : POSITION;
                float2 texcoord : TEXCOORD0;
            };

            struct v2f{
                float4 pos : SV_POSITION;
                float2 uv : TEXCOORD0;
            };  

            v2f vert (a2v v){
                v2f o;
                o.pos = UnityObjectToClipPos(v.pos);
                o.uv = TRANSFORM_TEX(v.texcoord, +_MainTex);
                return o;
            }

            fixed4 frag (v2f i) : SV_TARGET{
                int time = _Time.y * _Speed;
                // 行和列索引在一个行数没有限制的图集上运行，因为time不是连续的，所以索引也不是连续变换的
                float rowIndex = time / _HorizontalAmount;
                float colIndex = time - rowIndex*_HorizontalAmount;
                // 吧UV坐标从整体，调整到首个图像的位置上
                half2 uv = float2(i.uv.x /_HorizontalAmount, i.uv.y / _VerticalAmount);
                // 由于行索引在1，2，3，4阶跃，所以需要除以水平方向图片的个数，才能得到1/8，2/8的像素偏移。所以列索引也需要除以列上个数
                half2 xMov = colIndex / _HorizontalAmount;
                half2 yMov = rowIndex / _VerticalAmount;
                // 将UV坐标偏移到需要展示的小图片上
                uv.x += xMov;
                uv.y -= yMov;
                // 也可以使用下面简化的代码代替
                // half2 uv = i.uv + half2(rowIndex, -colIndex);
                // uv.x /= _HorizontalAmount;
                // uv.y /= _VerticalAmount;

                fixed4 col = tex2D(_MainTex, uv);
                col.rgb *= _Color;
                return col;
            }

            ENDCG
        }

    }
    FallBack "Transparent/VertexLit"
}
```

## 顶点动画  

顶点动画，就是通过在顶点着色器中对纹理顶点操作从而产生的的动画效果。
下面的代码实现了一个简单的波浪效果  

```cs
Shader "Custom/Shader"{
    Properties{
        _MainTex("纹理图片", 2D) = "white" {}
        _Color("颜色", Color) = (1,1,1,1)
        _Speed("流动速度", Range(0.1, 10.0)) = 1.0
        _WaveHeight("浪高", Range(0.1, 10.0)) = 1.0
        _WaveFrequence("波浪密度", Range(0.1,10)) = 1.0
    }

    SubShader {
        Tags { "RenderType" ="Transparent" "IgnoreProjector"="True" "Queue"="Transparent" "DisableBatching"="True"}
        pass{
            tags{"LightMode"="ForwardBase"}
            ZWrite Off
            Blend SrcAlpha OneMinusSrcAlpha
            Cull Off

            CGPROGRAM
            #pragma enable_d3d11_debug_symbols
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"


            sampler2D _MainTex;
            float4 _MainTex_ST;
            fixed4 _Color;
            float _Speed;
            float _WaveFrequence;
            float _WaveHeight;

            struct a2v{
                float4 vertex : POSITION;
                float2 texcoord : TEXCOORD0;
            };

            struct v2f{
                float4 pos:SV_POSITION;
                float2 uv:TEXCOORD0;
            };
            v2f vert(a2v v) {
                v2f o;

                float4 offset;
                offset.yzw = float3(0.0, 0.0, 0.0);

                // 书上的方式，整体没啥大的区别
                // offset.x = sin(1.0 * _Time.y + v.vertex.x * 10.0 + v.vertex.y * 10.0 + v.vertex.z * 10.0) * 1.0;
                // 实际上下面的方式就可以实现
                offset.x = sin(v.vertex.z * _WaveFrequence + _Time.y)*_WaveHeight;

                o.pos = UnityObjectToClipPos(v.vertex + offset * 0.4);
                o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);
                o.uv -=  float2(0.0, _Time.y * 0.045);

                return o;
            }
            fixed4 frag(v2f i):SV_TARGET{
                fixed4 col = tex2D(_MainTex, i.uv);
                col.rgb *= _Color;
                return col;
            }
            ENDCG
        }
    }
}
```

注意上面的实现中，我们还要使用一张特殊的模型才能实现，无法获取顶点信息  
![波浪效果所需模型](./IMG/11章/波浪效果模型.PNG)