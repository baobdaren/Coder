# 第八章 透明效果  

在unity中有两种方法实现透明效果。透明度测试和透明度混合。对于透明度测试则无法实现真正的半透明效果。

在没有透明效果的情况下，我们无需考虑模型的渲染顺序，这是因为深度缓冲的作用。在实时渲染中，深度缓冲用于解决可见性问题。它可以解决一个物体的哪些部分需要被渲染，那些部分会被其他物体遮挡。其基本思想就是：使用深度缓冲来判断该片元和相机的距离。当渲染一个片元时，需要把它的深度值和深度缓冲中的值相比较，如果它的值表明其距离相机更远，则会直接舍弃；而如果其距离相机更近，则会覆盖颜色缓冲中的值，并更新深度值（如果开启了深度写入）。  

* 透明度测试  
    采用极端的机制，只要一个片元不满足条件（通常是小于某个阈值），则会被直接舍弃。
* 透明度混合  
    这种方法可以得到真正的半透明效果。他会使用当前偏远的透明度和已经存储在颜色缓冲区中的颜色进行混合，从而得到新的颜色。但是，透明度混合需要关闭深度写入，这使得我们需要小心物体渲染的顺序。注意，透明度混合关闭了深度写入，没有关闭深度测试。所以对于透明度混合来说。  

## 8.2 unity shader 的渲染顺序  

Unity 为了解决渲染顺序的问题提供了渲染队列。  

|名称|索引|描述|
|:-:|:-:|:-:|
|Background|1000|最先渲染,通常是需要绘制在背景上的物体|
|Gemometry|2000|默认的渲染队列，大多数物体使用此队列，不透明物体使用此队列|
|Alpha Test|2450|需要透明度测试的物体|
|TransParent|3000|透明混合，在Geometry和Alphatest之后，再按`从后往前`依次渲染。任何使用了透明混合（例如关闭了深度写入）的物体都应该使用此队列|
|overlay|4000|该队列用于实现一些叠加效果。需要最后渲染的物体使用此队列|

比如我们想要使用透明度测试实现透明效果，则必须使用以下形式

```cs
Shader{
    Property{
        ...
    }
    SubShader{
        tags{"Queue"="AplhaTest"}
        pass{
            ...
        }
    }
}
```

比如我们需要使用透明度混合来实现半透明效果，则必须使用以下形式

```cs
Shader{
    Property{
        ...
    }
    SubShader{
        tags{"Queue"="Transparent"}
        pass{
            ZWrite Off
            ...
        }
    }
}
```

`ZWrite Off` 用于关闭深度写入，这里我们写在单个pass语句中。也可以写在subshader中，这意味着关闭该subshader下的所有pass的深度写入。  

使用透明度测试实现的透明效果

```cs
Shader "Custom/8/shader"
{
    Properties
    {
        _Color ("Color", Color) = (1, 1, 1, 1)
        _MainTex ("Main Tex", 2D) = "white" { }
        _CutOff ("Alpha Cutoff", Range(0, 1)) = 0.5
    }

    SubShader
    {
        // 使用alphatest渲染队列
        Tags { "RenderType" = "TransparentCutout" "Queue" = "AlphaTest" "IgnoreProjector" = "True" }
        pass
        {
            Tags { "LightMode" = "ForwardBase" }
            CGPROGRAM

            #pragma vertex vert
            #pragma fragment frag
            #include "Lighting.cginc"

            fixed4 _Color;
            sampler2D _MainTex;
            float4 _MainTex_ST;
            fixed _CutOff;

            struct a2v {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float4 texcoord : TEXCOORD0;// UV 坐标
            };

            struct v2f {
                float4 pos : SV_POSITION;
                float3 worldNormal : TEXCOORD0;
                float3 worldPos : TEXCOORD1;
                float2 uv : TEXCOORD2;
            };

            v2f vert (a2v v){
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.worldNormal = UnityObjectToWorldNormal(v.normal);
                o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
                o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);
                return o;
            }

            fixed4 frag (v2f i)  : SV_TARGET{
                fixed3 worldNormal = normalize(i.worldNormal);
                fixed3 WorldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));

                fixed4 fragColor = tex2D(_MainTex, i.uv);
                // 剔除alpha值小于设定阈值
                clip(fragColor.a - _CutOff);

                fixed3 albedo = _Color.rgb * fragColor.rgb;
                fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;
                fixed3 diffuse = _LightColor0.rgb * albedo * max(0, dot(worldNormal, WorldLightDir));

                return fixed4(ambient+diffuse, 1.0);
            }

            ENDCG

        }
    }
    FallBack "Transparent/Cutout/VertexLit"
}
```

使用透明度混合实现半透明效果

```cs
Shader "Custom/8/shader"
{
    Properties
    {
        _Color ("Color", Color) = (1, 1, 1, 1)
        _MainTex ("Main Tex", 2D) = "white" { }
        _AlpahScale ("Alpha Scale", Range(0, 1)) = 1
    }

    SubShader
    {
        Tags { "RenderType" = "TransparentCutout" "Queue" = "Transparent" "IgnoreProjector" = "True" }

        pass
        {
            Tags { "LightMode" = "ForwardBase" }
            ZWrite Off
            Blend SrcAlpha OneMinusSrcAlpha

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "Lighting.cginc"

            fixed4 _Color;
            sampler2D _MainTex;
            float4 _MainTex_ST;
            fixed _AlpahScale;

            struct a2v
            {
                float4 vertex: POSITION;
                float3 normal: NORMAL;
                float4 texcoord: TEXCOORD0;// UV 坐标
            };

            struct v2f
            {
                float4 pos: SV_POSITION;
                float3 worldNormal: TEXCOORD0;
                float3 worldPos: TEXCOORD1;
                float2 uv: TEXCOORD2;
            };

            v2f vert(a2v v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.worldNormal = UnityObjectToWorldNormal(v.normal);
                o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
                o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);
                return o;
            }

            fixed4 frag(v2f i): SV_TARGET
            {
                fixed3 worldNormal = normalize(i.worldNormal);
                fixed3 WorldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));

                fixed4 fragColor = tex2D(_MainTex, i.uv);
                // Alpha Test
                // clip(fragColor.a - _CutOff);

                fixed3 albedo = _Color.rgb * fragColor.rgb;
                fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;
                fixed3 diffuse = _LightColor0.rgb * albedo * max(0, dot(worldNormal, WorldLightDir));

                return fixed4(ambient + diffuse, fragColor.a * _AlpahScale);
            }
            ENDCG
        }
    }
    FallBack "Transparent/Cutout/VertexLit"
}
```

下面是以上代码的效果  

![8章/透明混合](./IMG/8章/透明混合.PNG)  
从上图看出，该结构在半透明情况下结构混乱，这是因为关闭了深度测试后，模型自身的片元在渲染时无法覆盖距离较远的片元在颜色缓冲区的值。这就需开启深度写入的透明混合。代码如下：  

```cs
Shader "Custom/8/shader"
{
    Properties
    {
        _Color ("Color", Color) = (1, 1, 1, 1)
        _MainTex ("Main Tex", 2D) = "white" { }
        _AlpahScale ("Alpha Scale", Range(0, 1)) = 1
    }

    SubShader
    {
        Tags { "RenderType" = "TransparentCutout" "Queue" = "Transparent" "IgnoreProjector" = "True" }
        pass{
            ZWrite On
            ColorMask 0
        }

        pass
        {
            Tags { "LightMode" = "ForwardBase" }
            ZWrite Off
            Blend SrcAlpha OneMinusSrcAlpha

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "Lighting.cginc"

            fixed4 _Color;
            sampler2D _MainTex;
            float4 _MainTex_ST;
            fixed _AlpahScale;

            struct a2v
            {
                float4 vertex: POSITION;
                float3 normal: NORMAL;
                float4 texcoord: TEXCOORD0;// UV 坐标
            };

            struct v2f
            {
                float4 pos: SV_POSITION;
                float3 worldNormal: TEXCOORD0;
                float3 worldPos: TEXCOORD1;
                float2 uv: TEXCOORD2;
            };

            v2f vert(a2v v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.worldNormal = UnityObjectToWorldNormal(v.normal);
                o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
                o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);
                return o;
            }

            fixed4 frag(v2f i): SV_TARGET
            {
                fixed3 worldNormal = normalize(i.worldNormal);
                fixed3 WorldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));

                fixed4 fragColor = tex2D(_MainTex, i.uv);
                // Alpha Test
                // clip(fragColor.a - _CutOff);

                fixed3 albedo = _Color.rgb * fragColor.rgb;
                fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;
                fixed3 diffuse = _LightColor0.rgb * albedo * max(0, dot(worldNormal, WorldLightDir));

                return fixed4(ambient + diffuse, fragColor.a * _AlpahScale);
            }

            ENDCG

        }
    }
    FallBack "Transparent/Cutout/VertexLit"
}
```

和透明混合代码相比，这里需要两个pass通道，第一个pass通道开启深度测试，但是不输出颜色，第二个通道由于深度测试已经完成，所以不会出现层次混乱问题了。  
![开启透明度测试的透明混合](./IMG/8章/开启深度写入的半透明效果.PNG)

双面渲染效果
这种效果可然我们看清透明体内部和背面

![双面渲染](./IMG/8章/双面渲染效果.PNG)